\chapter{Analysis of XQuery} \label{CHAPTER_analysis_of_xquery}
This chapter discusses selected constructs of XQuery language and denotes how they could be exploited in the XML schema inference process. It is divided into sections by particular domains of the inference.

Most of sample queries in this chapter are taken from \cite{Walmsley:2007:XQU:1205865} and \cite{Marchiori:07:XQU}, with or without modifications, as these were the main sources for this XQuery analysis.

\section{Structure of XML documents}
Most of queries can be exploited to obtain some information about the structure of respective XML documents. The structure of XML documents captures elements from these documents along with their names, attributes and their organization. What the root elements in these documents are, which elements can be contained within a certain element, whether they are optional or mandatory, etc.

Path expressions without predicates which use only child axis are the simplest example of such queries. For example, path expression \texttt{/bib/book/author} indicates that element \texttt{bib} is the root element and it contains one or more elements \texttt{book} and these contain one or more elements \texttt{author}.

Additional path expressions \texttt{/bib/book/title}, \texttt{//author/name} indicate that element \texttt{book} also contains element \texttt{title} and element \texttt{author} contains element \texttt{name}. The latter one uses also descendant axis. That means the query considers all elements \texttt{author} in the document, thus it is a hint that maybe there are several elements named \texttt{author} but with different absolute paths and some of them contain an element \texttt{name}.

Besides elements, attributes can be processed exactly in the same way. Path expression \texttt{/bib/book/@ISBN} indicates that element \texttt{book} can have attribute \texttt{ISBN}.

Statements of this kind are necessary for the XML schema inference, however their obtaining from queries is not significant due to the following reason. These statements could be determined directly from the XML documents and it could be easily done in a more convenient way. In addition, queries may not cover the whole relevant content of the documents. Let \texttt{E} be an element occurring in an XML document and let \texttt{Q} be an XQuery query. Consider the following cases:

\begin{enumerate}[1)]
\item \texttt{E} is directly mentioned in \texttt{Q}. For example elements \texttt{bib}, \texttt{book} and \texttt{author} are mentioned in path expression \texttt{/bib/book/author}.
\item \texttt{E} is not mentioned in \texttt{Q} but it does occur in a result of the evaluation of \texttt{Q}. For example path expression \texttt{/bib/book/author} returns elements \texttt{author} along with their subelements \texttt{name}, \texttt{birthdate} and \texttt{nationality}.
\item \texttt{E} is neither mentioned in \texttt{Q}, nor it occurs in the result, but it is processed by the evaluation of \texttt{Q}. For example the evaluation of path expression \texttt{//author} processes amongst others elements \texttt{bib} and \texttt{book}, in a search for elements \texttt{author}.
\item \texttt{E} occurs in a part of the XML document not related to \texttt{Q} at all.
\end{enumerate}

How we can obtain any information about element \texttt{E} from query \texttt{Q}? In case 1), \texttt{Q} can be directly exploited in the inference process. Case 2) requires the result of \texttt{Q} and additional processing of the result. Case 3) requires a step-by-step evaluation of \texttt{Q}, hence, the XML document is also required. And in case 4), it is not possible to obtain any information. 

In addition, these statements do not express any obligation of occurrence of elements and attributes nor clearly determine multiple occurrence of elements. We also cannot be sure that queries target nodes actually presented in the XML documents. Although query \texttt{/bib/book/author} indicates that element \texttt{author} is contained in element \texttt{book}, the query is valid whether this is true or not. In contrast, even basic methods of XML schema inference that utilize XML documents do not have these inadequacies.

On the other hand, the structural inference could be useful when the entire set of all XML documents is not available and the provided XML documents do not cover the structure completely.

\section{Number of Occurrences of Elements}
Some XQuery constructs indicate multiplicity of a particular element or limit the element to occur at most once. Consider the following query assuming variable \texttt{\$book1} is bound to a certain \texttt{book} element.

\begin{verbatim}
for $a in $book1/author 
order by $a/last, $a/first
return $a
\end{verbatim}

Apparently, this query expects more than one \texttt{author} element to be a child of the element the variable \texttt{\$book1} is bound to. Otherwise, any sorting would lack a reason. Although we cannot be absolutely sure about it again, assuming common-sense usage of XQuery, it is very likely that \texttt{author} element can occur multiple times as a subelement of element from variable \texttt{\$book1}.

\subsection{Multiple Occurrence}
A similar approach could be applied in many other situations. Another examples are particular usages of function \texttt{count()}, indexation, usage of set operators (\texttt{union}, \texttt{intersect}, \texttt{except}) and usage of function \texttt{one-or-more()}. Sample queries with a respective description follow.

\begin{verbatim}
<section_count>{ count(/book/section) }</section_count>
\end{verbatim}

Function \texttt{count()} returns the number of items in a provided sequence. If the sequence is a sequence of elements, the number of these elements will probably not be limited to one. The sample query indicates that the root element \texttt{book} can contain more than one element \texttt{section}.
An exception is usage of function \texttt{count()} in a predicate in expressions where it is used to determine if the number of some nodes is greater than zero. Often, this form is used to test presence of a certain node instead of actual counting of its occurrences. In this case, the node could be still limited to occur at most once.

\begin{verbatim}
($s/incision)[2]/instrument
($s/instrument)[position()>=2]
\end{verbatim}

Indexation of nodes and common usage of function \texttt{position()} suggest that the author of such query assumes a sequence of respective nodes.

\begin{verbatim}
one-or-more(/catalog/product[@id = 5]/color)
\end{verbatim}

In this query, the number of elements \texttt{color} in element \texttt{product} with attribute \texttt{id} equal to 5 has to be at least one, otherwise an error is raised upon execution. If we assume that this query is written correctly, with common sense and it should not raise the error, we can infer that element \texttt{product} has to contain one or more elements \texttt{color}.

\subsection{Occurrence Limited to One}
Contrary to the multiple occurrence, numerous XQuery constructs limit number of occurrences of an element to at most one or exactly one. Sample queries with description follow.

\begin{verbatim}
/catalog/product[1]/number lt 10
\end{verbatim}

\texttt{lt} is a representant of so called \emph{value comparison operators} (\texttt{eq}, \texttt{ne}, \texttt{lt}, \texttt{le}, \texttt{gt}, \texttt{ge}, see \cite{w3c_xquery}) which operate two sequences of zero or one item. If an operand of a value comparison operator is a sequence of more than one item, then a \emph{type error} (see \cite{w3c_xquery}) is raised.

\begin{verbatim}
for $item in //item 
order by $item/num 
return $item
\end{verbatim}

Alike the previous example, an expression in \texttt{order by} clause can be evaluated to at most one item or the \emph{type error} is raised. Therefore, every element \texttt{item} contains zero or one element \texttt{num} but not more.

Other similar examples are arithmetic expressions and functions accepting a sequence of at most one item. Function \texttt{zero-or-one()} will raise the \emph{type error} when supplied with a sequence of more than one item.

Those are constructs indicating limitation to zero or one occurrence. Function \texttt{exactly-one()} works similarly to the function \texttt{zero-or-one()} but accepts only sequences of exactly one item (which are in XQuery equal to this item itself).

\section{Element and Attribute Types}

\subsection{XML Schema Built-in Types}
XML Schema built-in types of elements and attributes can be inferred from the XML documents by analysing their content. Since the number of built-in types is 44 and inheritance is involved, such analysis may be imprecise, especially when a large enough set of XML documents is not available. This is the case when a set of XQuery queries may bring optimizations. For example, consider the following occurrences of element \texttt{a}.

\begin{verbatim}
<a>1</a>
<a>6</a>
<a>18</a>
\end{verbatim}

These three occurrences are not sufficient enough to determine the accurate type of element \texttt{a}, because values \texttt{1}, \texttt{6}, \texttt{18} are valid values of several types: \texttt{decimal}, \texttt{integer}, \texttt{byte}, \texttt{short}, \texttt{unsignedInt}, \texttt{positiveInteger} and many others.

If a value of an element or attribute is used in an expression, then this expression could be often exploited to determine a type of the value. Comparing the value to another value of a known type and supplying the value to a function call as an argument of a particular type are examples of these expressions as shown below.

\begin{verbatim}
//event/date = current-date()

/catalog/product/price < 24.5

declare function local:byteFunction($arg as xs:byte) as xs:byte
{...};
/catalog/product/local:byteFunction(@id)
\end{verbatim}

The determined types are \texttt{xs:date} for element \texttt{date}, \texttt{xs:decimal} for element \texttt{price} and \texttt{xs:byte} for attribute \texttt{id}.

Alongside common expressions, there are other XQuery constructs indicating types, such as \emph{type casting}, \emph{type constructors} and so called \emph{type declarations} (see \cite{w3c_xquery} for specification), demonstrated in the following examples.

Assuming variable \texttt{\$var} bound to a value of some element or attribute, the following two fractions of queries indicate its type to be \emph{xs:integer}.

\begin{verbatim}
$var cast as xs:integer
xs:integer($var)
\end{verbatim}

The latter one is usage of \emph{type casting}, the former one is \emph{type constructor}.

Similarly, the following two queries are examples of \emph{type declarations}. Value of element \texttt{number} is declared to be of type \texttt{xs:integer}.

\begin{verbatim}
every $number as element(*,xs:integer) in //number
  satisfies ($number > 0) 

declare variable $firstNumber as xs:integer 
:= data(//product/number[1]); 
\end{verbatim}

%Although determining built-in types of elements and attributes in simple queries may seem quite straightforward, reliable method would face some issues. More complex queries are commonly branched by \emph{if-then-else} construct. These branches can contain several contradictionary fragments from the view of type inference. Therefore, the reliable method would take the query structure into consideration and further research would be needed.
%\todo[inline]{Tady by to chtělo příklad. nebo to budeme řešit dál v navrženém algoritmu a toto je pouze diskuse případů?}

\subsection{Enumeration}
In several cases, XQuery \emph{if-then-else} construct may be used to branch the execution of query by all possible values of a certain variable. This is equivalent to \emph{switch} construct from other programming languages. When the control variable is bound to some element or attribute, type of this node can be inferred as enumeration and its individual values can be determined as well.

\begin{verbatim}
Query
let $cat := doc("catalog.xml")/catalog 
for $dept in distinct-values($cat/product/@dept) 
return <li>Department: {if ($dept = "ACC") 
                        then "Accessories" 
                        else if ($dept = "MEN") 
                             then "Menswear" 
                             else if ($dept = "WMN") 
                                  then "Womens" 
                                  else () 
  } ({$dept})</li> 

Results 
<li>Department: Womens (WMN)</li> 
<li>Department: Accessories (ACC)</li> 
<li>Department: Menswear (MEN)</li> 
\end{verbatim}

Finding such patterns in queries could be useful in combination with analysis of respective XML data. XML data could help to confirm or disprove this assumption based on the query analysis or vice-versa.

\section{Keys}

\subsection{Approach from \cite{Necasky:2009:DXK:1529282.1529414}}
Paper \cite{Necasky:2009:DXK:1529282.1529414} introduces a method of discovering keys and foreign keys by investigation of joins in queries. The basis of this discovery is a search for particular forms of joins, so called \emph{join patterns}, but the joins are processed only if they are found in a particular syntactic form. Therefore joins with the same semantics written in different syntax are not taken into consideration.

The following examples are queries that could be processed by a similar method, but the actual method will not use them.

\begin{verbatim}
<result>
  {
    for $u in doc("users.xml")//user_tuple
    for $i in doc("items.xml")//item_tuple
    where $u/rating > "C" 
       and $i/reserve_price > 1000 
       and $i/offered_by = $u/userid
    return
        <warning>
            { $u/name }
            { $u/rating }
            { $i/description }
            { $i/reserve_price }
        </warning>
  }
</result>
\end{verbatim}

Clause \texttt{where} is used for the join condition \texttt{\$i/offered\_by = \$u/userid instead} of the join condition in predicate of the second \texttt{for} expression.

\begin{verbatim}
<result>
  {
    for $i in doc("items.xml")//item_tuple
    where empty(doc("bids.xml")//bid_tuple[itemno = $i/itemno])
    return
        <no_bid_item>
            { $i/itemno }
            { $i/description }
        </no_bid_item>
  }
</result>
\end{verbatim}

This query illustrates a join where the value of one of the joined elements is not required, thus the knowledge of its existence is sufficient. Therefore the query does not have to contain the second \texttt{for} or \texttt{let} keyword and its expression can be moved to \texttt{where} clause.

\begin{verbatim}
for $item in doc("order.xml")//item, 
    $product in doc("catalog.xml")//product, 
    $price in doc("prices.xml")//prices/priceList/prod 
where $item/@num = $product/number and $product/number = $price/@num 
return <item num="{$item/@num}" 
         name="{$product/name}" 
         price="{$price/price}"/>
\end{verbatim}

A common three-way join can be also utilized to infer keys and foreign keys, however, the respective XML data and their analysis would be needed to tell what is a key and what is a foreign key.

\subsection{Join of Self-referencing Data}
By the term ``self-referencing data'' are meant XML elements that somehow reference items from the same set. Example of a query that operates upon such data follows.

\begin{verbatim}
declare function local:one_level($p as element()) as element()
{
  <part partid="{ $p/@partid }" name="{ $p/@name }" >
    {
      for $s in doc("partlist.xml")//part
      where $s/@partof = $p/@partid
      return local:one_level($s)
    }
  </part>
};

<parttree>
  {
    for $p in doc("partlist.xml")//part[empty(@partof)]
    return local:one_level($p)
  }
</parttree>
\end{verbatim}

Apparently, element \texttt{part} can contain attributes \texttt{partid} and \texttt{partof}. Elements with unspecified attribute \texttt{partof} are at the top of the recursive hierarchy, while each element with this attribute specified references an element with attribute \texttt{partid} of the same value.

By using a similar approach as described in \cite{Necasky:2009:DXK:1529282.1529414} attribute \texttt{partid} can be marked as a key and attribute \texttt{partof} as its foreign key.

\subsection{Negative Statements about Uniqueness} \label{Negative_uniqueness}
In many cases a statement refusing uniqueness of element or attribute values can be inferred. Such statements may be helpful in combination with other methods in its process of making a decision whether a particular element or attribute is unique or not.

Basic representant is a common \emph{FLWOR} query.

\begin{verbatim}
<bib>
  {
    for $b in doc("http://bstore.example.com/bib.xml")//book
    where $b/publisher = "abcde" and $b/@year > 2000
    order by $b/title
    return
        <book>
            { $b/@year }
            { $b/title }
        </book>
  }
</bib>
\end{verbatim}

Usage of \texttt{for} construct indicates that a sequence of \texttt{book} elements which satisfy the condition in \texttt{where} clause is expected. It is a condition composed of two single conditions joined by \texttt{and} logical operator. Therefore, in order to satisfy the whole condition, both single conditions must be satisfied as well. Thus, it is expected that several elements satisfy each of the single conditions.

The first of them is a test of equality of book's subelement \texttt{publisher} to a string literal. Based on the expectation, element \texttt{publisher} cannot be unique. However, the second condition is greater-than comparison of \texttt{year} attribute to an integer literal, it cannot be inferred whether this attribute is unique or not. The reason is that even if it was unique, there still might be more than one \texttt{book} element meeting this condition. Also, any statement, positive nor negative, cannot be inferred about \texttt{title} subelement.

Other simple examples are passing a result of basic path expression to a call of \texttt{distinct-values()} function and usage of \emph{aggregation} functions.

\begin{verbatim}
<results>
  {
    let $doc := doc("prices.xml")
    for $t in distinct-values($doc//book/title)
    let $p := $doc//book[title = $t]/price
    return
      <minprice title="{ $t }">
        <price>{ min($p) }</price>
      </minprice>
  }
</results>
\end{verbatim}

According to the use of \texttt{distinct-values()} function, it can be easily seen that the author of this query assumes possible occurrence of more than one \texttt{book} element with the same value of their \texttt{title} subelement. Thus, \texttt{title} element is not unique.

Alike, variable \texttt{\$p} is bound to a price of each title and then passed to \texttt{min()} function call. That indicates that one book title is supposed to have several prices, however, it cannot be said if a certain occurrence of element \texttt{book} can contain more than one \texttt{price} subelement.

Also, many other types of queries could be exploited to obtain negative uniqueness statements like occurrence of element or attribute in \texttt{stable order by} clause or selection of an element set based on a value of particular attribute and consecutive treatment of this set as a sequence of elements.

\begin{verbatim}
let $prods := doc("catalog.xml")//product 
for $prod in $prods 
where $prod << $prods[@dept = $prod/@dept][last()] 
return $prod 
\end{verbatim}

Usage of predicate \texttt{[last()]} indicates that \texttt{\$prod} is a sequence of elements. 

\subsection{Uniqueness}
Contrary to non-uniqueness discovery, some XQuery constructs can indicate uniqueness of elements or attributes; however, it seems to be more difficult. One of the approaches of uniqueness discovery could be an investigation of what the query does return. Consider the following query.

\begin{verbatim}
for $product in /catalog/product
let $number := $product/number
return <prod xml:id="{concat('prod', number)}"/>
\end{verbatim}

For each \texttt{product} element, new element \texttt{prod} with attribute \texttt{xml:id} is created. Since attribute \texttt{xml:id} is supposed to be unique and there is a direct transformation of the values of \texttt{number} elements to the values of attribute \texttt{xml:id}, it is very likely that element \texttt{number} is unique in the source data.

\section{Other Constructs}

\subsection{XML Schema \texttt{xs:sequence} and \texttt{xs:all} constructs}
If an order of appearance of some element set, for example subelements of a certain element, is important, this can be expressed by XML Schema construct \texttt{xs:sequence}. On the other hand, \texttt{xs:all} is involved when the elements may occur in any order.

If there is a large enough set of XML documents available, it should be possible to correctly detect where to use \texttt{xs:sequence} and \texttt{xs:all} constructs (paper \cite{Vosta:2008:EAC:1802514.1802522} deals with this problem). However, if the inference is made using a smaller set of XML documents, it may happen that every occurrence of some element set is in the same order but the input data are not representative enough to be sure. Comparison of element order in queries can help to decide for the use of \texttt{xs:sequence}.

Every two elements in an XML document that are not siblings with the same name and none of them is the root element have the nearest common ancestor determining their relative order. If there is a lack of evidence to choose between the two constructs in the ancestor, then available queries can be searched for an order comparison of these two elements like in the following query.

\begin{verbatim}
let $i := //incision[2]
for $a in //action[. >> $i]
return $a//instrument
\end{verbatim}

Apparently, the relative order of elements \texttt{incision} and \texttt{action} is important, because the query composes the result using only those \texttt{action} elements that succeed the second \texttt{incision} element. Therefore, in a respective type definition part of their nearest common ancestor \texttt{xs:sequence} should be used in favour of \texttt{xs:all}.

\subsection{Intermediate XML structure}
Intermediate XML structure represents XML data that are neither read from input XML documents nor created as an output of queries but they are somehow used by the queries. Objective of this section is intermediate XML structure created directly in the queries. It may serve to various purposes and it can be created in various ways, hard-coded in queries, computed from the input XML documents to simplify their structure. The former is demonstrated in the following example.

\begin{verbatim}
let $deptNames := <deptNames> 
                    <dept code="ACC" name="Accessories"/> 
                    <dept code="MEN" name="Menswear"/> 
                    <dept code="WMN" name="Womens"/> 
                  </deptNames> 
let $catalog := doc("catalog.xml")/catalog 
for $dept in distinct-values($catalog/product/@dept) 
return <li>Department:
         {data($deptNames/dept[@code = $dept]/@name)} ({$dept})
       </li> 
\end{verbatim}

Intermediate XML structure in this query can be used to identify possible values of attribute \texttt{dept} in \texttt{product} element and therefore determine its type as enumeration.

This is a very simple example of intermediate structure. Since there are admittedly many means of utilization of intermediate structure, further research would be needed.